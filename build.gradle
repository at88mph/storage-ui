plugins {
    id 'war'

    // IntelliJ IDEA plugin here to allow integration tests to appear properly in IDEs.
    id 'idea'
}

repositories {
    mavenLocal()
    jcenter()
}

dependencies {

    implementation 'org.opencadc:cadc-vosui:1.1.43'
    implementation 'org.opencadc:cadc-registry:[1.5.2,)'
    providedCompile 'javax.servlet:javax.servlet-api:[3.1,)'
    providedCompile 'javax.servlet:javax.servlet-api:[3.0,)'

    testImplementation 'junit:junit:[4.12,)'
    testImplementation 'org.opencadc:cadc-web-test:[2.1.1, )'
    testImplementation 'org.seleniumhq.selenium:selenium-java:[3.14,)'
}

sourceCompatibility = '1.8'
version = '1019'

war {
    archiveName 'storage##' + project.version + '.war'
}

ext {
    // Change this value to test other locations, such as Production or RC.
    intTest_default_web_app_url = 'http://localhost:8080'
    intTest_user_name = 'CADCtest'
    intTest_user_password = 'sywymUL4'
}

sourceSets {
    intTest
}

configurations {
    intTestCompile
    intTestCompile.extendsFrom(testCompile)
}

idea {
    module {
        //and some extra test source dirs
        testSourceDirs += file('src/intTest/java')
        scopes.TEST.plus += [ configurations.intTestCompile ]
    }
}

['firefox', 'chrome'].each { driver ->
    task "intTest${driver.capitalize()}"(type: Test) { driverTest ->
        testClassesDirs = sourceSets.intTest.output.classesDirs
        classpath = sourceSets.intTest.runtimeClasspath
        reports {
            html.destination = reporting.file("$name/html")
        }
        dependencies {
            intTestCompile 'junit:junit:[4.12,)'
            intTestCompile 'org.opencadc:cadc-web-test:[2.1.1,)'
            intTestCompile 'org.seleniumhq.selenium:selenium-java:[3.14,)'
        }

        systemProperty "driver", "${driver}"

        if (project.hasProperty('intTest_selenium_server_url')) {
            systemProperty 'selenium.server.url', project.intTest_selenium_server_url
            logger.info('Running browser tests on ' + project.intTest_selenium_server_url)
        }
        else {
            systemProperty 'selenium.server.url', 'http://cadcint2.cadc.dao.nrc.ca:3444'
        }

        if (project.hasProperty('intTest_web_app_url')) {
            systemProperty 'web.app.url', project.intTest_web_app_url
        }
        else {
            systemProperty 'web.app.url', project.intTest_default_web_app_url
        }

        systemProperty 'user.name', project.intTest_user_name
        systemProperty 'user.password', project.intTest_user_password

        maxParallelForks = 1
    }
}
